###########################################################
#   ______  _____  ______  ______ __   __ ______  _____   #
#   |_____]   |   |  ____ |  ____   \_/   |_____]   |	  #
#   |_____] __|__ |_____| |_____|    |    |_____] __|__   #
#												   zshrc  #
###########################################################

# environment
if [ -n "$ZSH_ENV" ]; then source "$ZSH_ENV"; fi

# aliases
if [ -f "$HOME/.zsh_aliases" ]; then source "$HOME/.zsh_aliases"; fi

# command not found (package suggestion)
if [[ -s '/etc/zsh_command_not_found' ]] then
	source '/etc/zsh_command_not_found'; fi

# bell off
unsetopt BEEP

# Environment variables

## Applications
export VISUAL=nvim
if [ -n "$TMUX" ] && [ $VISUAL = 'vim' ] ; then
	VISUAL=(vim -T xterm-256color)
	export MANPAGER="vim -T xterm-256color -M +MANPAGER -"
fi
export EDITOR=(vi -e)
export BROWSER=firefox
export JAVA_HOME=/usr/lib/jvm/java-11-openjdk-amd64/

## PATH
export GOPATH=$HOME/bin/go
export PATH=$PATH:$HOME/bin
PATH+=:$HOME/bin/tmux
PATH+=:/usr/lib
PATH+=:~/.npm-global/bin
PATH+=:/home/linuxbrew/.linuxbrew/bin
PATH+=:$GOPATH/bin
PATH+=:~/bin/norminette
PATH+=:$JAVA_HOME

## Paths
export PROG="$HOME/Work/prog/"
export DOT="$HOME/dotfiles"
export LFT="$HOME/42/libft/"
export HOME_VID="$HOME/Videos"
export HOME_PIC="$HOME/Pictures"
export HOME_GAM="$HOME/Games"
export HOME_DOC="$HOME/Documents"
export HOME_MUS="$HOME/Music"
export HOME_42="$HOME/Work"
export DATAPATH="/media/data/"

# disable shell scroll interrupt
stty -ixon

# history
export HISTFILE=~/.zsh_history
export SAVEHIST=1000000
export HISTSIZE=1000000                 # session history size
export HISTFILESIZE=1000000             # history file size
export HISTTIMEFORMAT="[%F %T] "
export HISTIGNORE="&:ls:bg:fg:exit"		# ignore some commands

setopt EXTENDED_HISTORY                 # save start time and duration
setopt INC_APPEND_HISTORY               # update history when command is issued
setopt HIST_IGNORE_ALL_DUPS             # hide duplicates in history
setopt HIST_IGNORE_SPACE                # hide commands starting with spaces

# bash style word (no '/')
autoload -U select-word-style
select-word-style bash

# autocompletion
autoload -Uz compinit promptinit
compinit
promptinit

zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)(?)*==94=34}:${(s.:.)LS_COLORS}")';
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' #  case insensitive
zstyle ':completion:*' menu selectzmodload                # use menu
setopt no_list_ambiguous                                  # complete then show menu

# vi mode
bindkey -v
KEYTIMEOUT=1

autoload edit-command-line
zle -N edit-command-line

# insert mode mappings
bindkey '^p' edit-command-line
bindkey '^b' backward-char
bindkey '^f' forward-char
bindkey '^a' beginning-of-line
bindkey '^e' end-of-line
bindkey '^j' history-search-forward
bindkey '^k' history-search-backward
bindkey '^[[B' history-search-forward
bindkey '^[[A' history-search-backward
bindkey '^l' clear-screen
bindkey '^o' expand-or-complete
bindkey '^n' accept-and-infer-next-history
bindkey '^u' backward-kill-word
bindkey '^ ' autosuggest-accept

bindkey 'è' vi-backward-char
bindkey 'ê' history-search-forward
bindkey 'ë' history-search-backward
bindkey 'ì' vi-forward-char

# complete menu mode mappings
zmodload zsh/complist

bindkey -M menuselect 'è' vi-backward-char
bindkey -M menuselect 'ê' vi-down-line-or-history
bindkey -M menuselect 'ë' vi-up-line-or-history
bindkey -M menuselect 'ì' vi-forward-char
bindkey -M menuselect '^[y' accept-search
bindkey -M menuselect '^[m' accept-search
bindkey -M menuselect '^y' accept-search
bindkey -M menuselect '^o' reverse-menu-complete

# normal mode mappings
bindkey -a 'H' beginning-of-line
bindkey -a 'L' end-of-line

autoload -U colors && colors

__get_vi_mode_bottom() {
	local vimstyle
	vimstyle='%{\e[01;01;92m%} > %{\e[0m%}'
	if [ "$KEYMAP" = "vicmd" ]
	then
		vimstyle='%{\e[00;01;93m%} < %{\e[0m%}'
	fi
	print -n -- $vimstyle
}

__get_pia_state() {
	local piastate=$(piactl get connectionstate)
	if [ ${piastate:0:1} = 'C' ] ; then
		print -n -- 'PIA up'
	else
	print -n -- 'PIA down'
	fi
}

DEFAULT_VI_MODE=viins

zle-keymap-select() {
	zle reset-prompt
}
zle -N zle-keymap-select

zle-line-init() {
	zle -K $DEFAULT_VI_MODE
}
zle -N zle-line-init

git_branch() {
	git symbolic-ref --short HEAD 2> /dev/null
}

setopt prompt_subst
precmd() {
	psvar[1]=$(git_branch)
}

PROMPT=$'\n '
if [ "$(id -u)" = 0 ] ; then
	PROMPT+=$'%{\e[01;31;100m%}  %n %{\e[0m%}'  # root user
else
	PROMPT+=$'%{\e[01;36;100m%}  %n %{\e[0m%}'  # user
fi
PROMPT+=$'%{\e[01;39;100m%} - %{\e[0m%}'    # separation
if [ -n "$SSH_CLIENT" ] ; then
	PROMPT+=$'%{\e[01;33;100m%} %M  %{\e[0m%} ' # ssh host
else
	PROMPT+=$'%{\e[01;32;100m%} %M  %{\e[0m%} ' # host
fi
PROMPT+=$'%{\e[00;01;93m%} %~%{\e[0m%} '    # cwd
PROMPT+=$'%{\e[00;01;95m%}%1v%{\e[0m%} '    # branch
# NOTE: too slow
# PROMPT+=$'$(__get_pia_state)'
PROMPT+=$'\t${promt_cmd_info}'
PROMPT+=$'%{$reset_color%}'
PROMPT+=$'\n$(__get_vi_mode_bottom)'
PROMPT+=$'%{$reset_color%}'                 # end

# rprompt

prompt_timer_preexec() {
	prompt_timer_preexec_ms=${(%):-%D{%s%3.}}
}

prompt_timer_precmd() {
	if (( prompt_timer_preexec_ms )); then
		local -ri now_ms=${(%):-%D{%s%3.}}
		local -ri elapsed_ms=$(( now_ms - prompt_timer_preexec_ms ))
		local -ri elapsed_s=$(( elapsed_ms/1000 ))
		local -ri ms=$(( elapsed_ms%1000 ))
		local -ri s=$(( elapsed_s%60))
		local -ri m=$(( (elapsed_s/60)%60 ))
		local -ri h=$(( elapsed_s/3600 ))
		if (( h > 0 )); then
			command_time=${h}h${m}m
		elif (( m > 0 )); then
			command_time=${m}m${s}s
		elif (( s >= 10 )); then
			command_time=${s}.${(l:2::0:)$(( ms/10 ))}s # 12.34s
		elif (( s > 0 )); then
			command_time=${s}.${(l:3::0:)ms}s # 1.234s
		else
			command_time=${ms}ms
		fi
		promt_cmd_info=$' %(?.%{\e[00;01;92m%}[0.%{\e[00;01;91m%}[%?)'\ $'%{\e[00;01;94m%}'${command_time}
		promt_cmd_info+=$'%(?.%{\e[00;01;92m%}.%{\e[00;01;91m%})]'
		unset prompt_timer_preexec_ms
	else
		# Clear previous result on empty command
		unset promt_cmd_info
	fi
}

setopt nopromptbang prompt{cr,percent,sp,subst}

autoload -Uz add-zsh-hook
add-zsh-hook preexec prompt_timer_preexec
add-zsh-hook precmd prompt_timer_precmd

# Color man pages in `less`
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

# Launch base 16 theme
export BASE16_SHELL="$DOT/shells/themes/base16-shell"
if [ -d $BASE16_SHELL ]; then
	[ -n "$PS1" ] && \
		[ -s "$BASE16_SHELL/profile_helper.sh" ] && \
		eval "$("$BASE16_SHELL/profile_helper.sh")"
fi

# Plugin Sourcer
source "$DOT/shells/zsh/plugins/zsh-pluginsourcer/zsh-pluginsourcer.zsh"

zvm_after_init_commands+=('[ -f $DOT/fzf.zsh ] && source $DOT/fzf.zsh')
zvm_after_init_commands+=('bindkey "^j" history-search-forward')
zvm_after_init_commands+=('bindkey "^k" history-search-backward')
zvm_after_init_commands+=('bindkey "^n" accept-and-infer-next-history')
